buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath files('libs/gradle-witness.jar')
    }
}

plugins {
    // Base
    id 'java'

    // Publishing on bintray
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7'
}

apply plugin: 'witness'

targetCompatibility = '1.7'
sourceCompatibility = '1.7'

// In this section you declare where to find the dependencies of your project
repositories {
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.21'

    // Dependency: Msgpack serialization
    compile group: 'org.json', name: 'json', version: '20160212'
    compile 'org.msgpack:msgpack-core:0.8.11'
    compile 'org.msgpack:jackson-dataformat-msgpack:0.8.11'

    // Dependency: WebSockets
    compile 'com.neovisionaries:nv-websocket-client:1.28'

    // Dependency: Binary chunking
    compile 'org.saltyrtc.chunked-dc:chunked-dc:0.2.0'

    // Test dependencies
    testCompile 'org.slf4j:slf4j-simple:1.7.21'
    testCompile 'junit:junit:4.12'
}

dependencyVerification {
    verify = [
            'org.slf4j:slf4j-api:1d5aeb6bd98b0fdd151269eae941c05f6468a791ea0f1e68d8e7fe518af3e7df',
            'org.json:json:0aaf0e7e286ece88fb60b9ba14dd45c05a48e55618876efb7d1b6f19c25e7a29',
            'org.msgpack:msgpack-core:2a92184bb2d470656a7388c204221ce2e6dd28cd0cac8dae700656a06699e8bb',
            'org.msgpack:jackson-dataformat-msgpack:ea532e53a01fef5f8b0c3d86a18124433baa69bdee5ccd5d9616ed5cb3237d37',
            'com.neovisionaries:nv-websocket-client:66eac33767a2aa720feeb547666da98cd9b4493b3ba8bcded721b06cc90e8156',
            'com.fasterxml.jackson.core:jackson-databind:215022eed86bde62e8ff5e9b04297a6a7a896df89c80576aa2291e5e1142a94b',
            'com.fasterxml.jackson.core:jackson-annotations:f775d20b6ad1ef122f453b2d7aeebb009523f7cdf89a674fe2f303221c95696b',
            'com.fasterxml.jackson.core:jackson-core:526ab629da103bf2c93e72bd541d7c34e498182460c56d5f48b03dbac91b1df9',
            'org.saltyrtc.chunked-dc:chunked-dc:e32ae9c0df9b2b3e5b3d2c8699c966b1d0fad89f76d2cab4be60268567415631',
    ]
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardError"
    }
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        SaltyRTC(MavenPublication) {
            from components.java
            groupId 'org.saltyrtc.client'
            artifactId 'saltyrtc-client'
            version '0.6.1'
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['SaltyRTC']
    pkg {
        repo = 'maven'
        name = 'saltyrtc-client'
        userOrg = 'saltyrtc'
        desc = 'Java client for SaltyRTC.'
        websiteUrl = 'http://saltyrtc.org/'
        issueTrackerUrl = 'https://github.com/saltyrtc/saltyrtc-client-java/issues'
        vcsUrl = 'https://github.com/saltyrtc/saltyrtc-client-java.git'
        githubRepo = 'saltyrtc/saltyrtc-client-java'
        publicDownloadNumbers = true
        licenses = ['MIT', 'Apache-2.0']
        labels = ['saltyrtc', 'webrtc', 'ortc', 'data channels', 'signaling', 'secure']
        version {
            name = '0.6.1'
            desc = 'SaltyRTC Java Client v0.6.1'
            released = new Date()
            vcsTag = 'v0.6.1'
        }
    }
}
