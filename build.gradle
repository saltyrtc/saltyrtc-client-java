buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath files('libs/gradle-witness.jar')
    }
}

plugins {
    // Base
    id 'java'

    // Publishing on bintray
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7'
}

apply plugin: 'witness'
apply plugin: 'maven'

def app_version = '0.9.3-beta1'

targetCompatibility = '1.7'
sourceCompatibility = '1.7'

// In this section you declare where to find the dependencies of your project
repositories {
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.21'

    // Dependency: Msgpack serialization
    compile group: 'org.json', name: 'json', version: '20160810'
    compile 'org.msgpack:msgpack-core:0.8.12'
    compile 'org.msgpack:jackson-dataformat-msgpack:0.8.12'

    // Dependency: WebSockets
    compile 'com.neovisionaries:nv-websocket-client:1.31'

    // Dependency: Binary chunking
    compile 'org.saltyrtc.chunked-dc:chunked-dc:1.0.0'

    // Test dependencies
    testCompile 'org.slf4j:slf4j-simple:1.7.21'
    testCompile 'junit:junit:4.12'
}

dependencyVerification {
    verify = [
            'org.slf4j:slf4j-api:1d5aeb6bd98b0fdd151269eae941c05f6468a791ea0f1e68d8e7fe518af3e7df',
            'org.json:json:bf51c9013128cb15201225e51476f60ad9116813729040655a238d2829aef8b8',
            'org.msgpack:msgpack-core:0d7058c6ffeefbb47a4c80ceba0526931b39944291ebd134cac84882aa515721',
            'org.msgpack:jackson-dataformat-msgpack:387eb22a21f21a3797665d1f721be0a255eed80be2281c27b59dccc7d10817c7',
            'com.neovisionaries:nv-websocket-client:1abdf67bcdacff34210a4d914b47836021097d28dbbceb4d559c73e1054b83a4',
            'com.fasterxml.jackson.core:jackson-databind:215022eed86bde62e8ff5e9b04297a6a7a896df89c80576aa2291e5e1142a94b',
            'com.fasterxml.jackson.core:jackson-annotations:f775d20b6ad1ef122f453b2d7aeebb009523f7cdf89a674fe2f303221c95696b',
            'com.fasterxml.jackson.core:jackson-core:526ab629da103bf2c93e72bd541d7c34e498182460c56d5f48b03dbac91b1df9',
            'org.saltyrtc.chunked-dc:chunked-dc:32e2b3f90ba64057e43acc49c3d7c33f1cc5492bfadebcfdfcf2de603b415948',
    ]
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardError"
    }
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        SaltyRTC(MavenPublication) {
            from components.java
            groupId 'org.saltyrtc.client'
            artifactId 'saltyrtc-client'
            version app_version
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

// Upload to Bintray
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['SaltyRTC']
    pkg {
        repo = 'maven'
        name = 'saltyrtc-client'
        userOrg = 'saltyrtc'
        desc = 'Java client for SaltyRTC.'
        websiteUrl = 'http://saltyrtc.org/'
        issueTrackerUrl = 'https://github.com/saltyrtc/saltyrtc-client-java/issues'
        vcsUrl = 'https://github.com/saltyrtc/saltyrtc-client-java.git'
        githubRepo = 'saltyrtc/saltyrtc-client-java'
        publicDownloadNumbers = true
        licenses = ['MIT', 'Apache-2.0']
        labels = ['saltyrtc', 'webrtc', 'ortc', 'data channels', 'signaling', 'secure']
        version {
            name = app_version
            desc = 'SaltyRTC Java Client v${app_version}'
            released = new Date()
            vcsTag = 'v${app_version}'
        }
    }
}

// Upload to local Maven repo for testing
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('/tmp/maven/'))
            pom.version = app_version
            pom.groupId = 'org.saltyrtc.client'
            pom.artifactId = 'saltyrtc-client'
        }
    }
}
